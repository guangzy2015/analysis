buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'io.spring.gradle:dependency-management-plugin:1.0.0.RELEASE'
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.5.1.RELEASE'
    }
}

apply plugin: 'java'

sourceCompatibility = 1.8

group 'com.feng.analysis'
version '1.0.0-SNAPSHOT'

configure(subprojects) {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'findbugs'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'eclipse'
    apply plugin: 'jacoco'

    compileJava {
        options.encoding = 'UTF-8'
        options.compilerArgs << "-parameters" << '-Xlint:unchecked'
    }
    compileTestJava {
        options.encoding = 'UTF-8'
        options.compilerArgs << "-parameters" << '-Xlint:unchecked'
    }

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    repositories {
        mavenCentral()
    }
    dependencyManagement {
        imports {
            mavenBom 'io.spring.platform:platform-bom:Athens-SR3' //
            mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Camden.SR6'
        }
    }
    configurations {
        //全局禁用slf4的配置，改为log4j2
        compile.exclude group:'org.springframework.boot', module: 'spring-boot-starter-logging'
        compile.exclude group:'org.slf4j', module: 'slf4j-log4j12'
        compile.exclude group:'org.slf4j', module: 'jcl-over-slf4j'
        compile.exclude group:'org.slf4j', module: 'log4j-over-slf4j'
        compile.exclude group:'org.slf4j', module: 'jul-to-slf4j'
        compile.exclude group:'org.springframework.boot', module: 'spring-boot-starter-redis'
    }
    dependencies {
        compile 'org.projectlombok:lombok:1.16.12',
                'com.google.code.findbugs:annotations:3.0.1'
        compile("org.springframework.boot:spring-boot-starter") {//spring-boot容器支持
            exclude group:'org.springframework.boot', module: 'spring-boot-starter-logging'
        }
        compile ("org.springframework.boot:spring-boot-starter-log4j2:1.5.1.RELEASE"){//容器支持
        }
        compile("org.springframework.boot:spring-boot-starter-test") {//容器支持
        }
        compile("org.springframework.boot:spring-boot-starter-aop") {//容器支持
        }

        //jackson datatype 支持jdk8 time
        compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.6.0"
        //mybatis 支持jdk8 time
        compile 'org.mybatis:mybatis-typehandlers-jsr310:1.0.1'

        compile 'org.springframework.boot:spring-boot-starter-actuator'
        //spring batch
        compile 'org.springframework.boot:spring-boot-starter-batch'

        //配置属性支持 @ConfigurationProperties(prefix = "xxx")
        compile('org.springframework.boot:spring-boot-configuration-processor') {
        }

        //spring 事务支持
        compile 'org.springframework:spring-context-support'
        compile 'org.springframework:spring-orm'
        compile 'org.springframework:spring-tx'
        compile 'org.springframework:spring-jdbc'

        //mysql驱动
        compile 'mysql:mysql-connector-java'
        //mybatis
        compile 'org.mybatis:mybatis-spring:1.3.0',
                'org.mybatis:mybatis:3.4.0'
        //datasource
        compile 'com.dangdang:sharding-jdbc-core:1.4.2'
        compile 'com.alibaba:druid:1.0.12'

        //commons
        compile 'org.apache.commons:commons-lang3',
                'org.apache.commons:commons-collections4:4.1',
                'org.apache.commons:commons-math3:3.6.1'

        testCompile 'org.testng:testng',
                'org.uncommons:reportng:1.1.4'
        testCompile 'com.google.inject:guice',
                'org.springframework.boot:spring-boot-starter-test',
                'com.ninja-squad:DbSetup:2.1.0',
                'org.jmockit:jmockit:1.27',
                'com.google.truth:truth:0.28',
                'com.h2database:h2:1.4.192',
                'com.github.springtestdbunit:spring-test-dbunit:1.2.1',
                'org.dbunit:dbunit:2.5.1',
                'org.apache.commons:commons-pool2:2.3'

        //日志 log4j2和sf4j整合
        compile 'org.apache.logging.log4j:log4j-slf4j-impl:2.7'
        compile 'org.apache.logging.log4j:log4j-api:2.7'
        compile 'org.apache.logging.log4j:log4j-core:2.7'
        compile 'commons-logging:commons-logging'
    }
    test {
        systemProperties System.properties
        systemProperties['user.dir'] = workingDir

        useTestNG() {
            suites 'src/test/resources/testng.xml'
        }

        options {
            listeners << 'org.uncommons.reportng.HTMLReporter'
            listeners << 'org.uncommons.reportng.JUnitXMLReporter'
        }
    }

    jacoco {
        toolVersion = '0.7.6.201602180812'
    }

    task wrapper(type: Wrapper) {
        gradleVersion '3.3'
    }

}

configure(subprojects.findAll { it.name == 'analysis-app' }) {
    //repackage
    apply plugin: 'spring-boot'
    jar {
        baseName = 'analysis-app'
        version  = ''
        manifest {
            attributes('Implementation-Version': project.version,
                    'Main-Class': "com.feng.analysis.app.AnalysisApp"
            )
        }
        exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
    }
}
